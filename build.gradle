plugins {
    id "org.asciidoctor.jvm.convert" version "3.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "org.jetbrains.kotlin.kapt" version "1.6.10"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.6.10"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "org.sonarqube" version "3.3"
    id "application"
    id 'maven-publish'
}

version "0.0.17-SNAPSHOT"
group "technology.iatlas.spaceup"

repositories {
    mavenCentral()
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
    jcenter()
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    kapt(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    kapt("io.micronaut:micronaut-inject-java:$micronautVersion")
    kapt("io.micronaut:micronaut-validation:$micronautVersion")
    kapt("io.micronaut:micronaut-graal:$micronautVersion")
    kapt("io.micronaut.openapi:micronaut-openapi:3.2.0")
    kapt("io.micronaut.security:micronaut-security-annotations:3.2.0")
    compileOnly(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    compileOnly('org.graalvm.nativeimage:svm:21.2.0')
    implementation(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    implementation("io.micronaut:micronaut-inject:$micronautVersion")
    implementation("io.micronaut:micronaut-validation:$micronautVersion")
    implementation('io.micronaut.rxjava3:micronaut-rxjava3:2.1.1')
    implementation("io.reactivex.rxjava3:rxkotlin:3.0.1")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    implementation("io.micronaut.kotlin:micronaut-kotlin-runtime:3.0.0")
    implementation("io.micronaut:micronaut-runtime:$micronautVersion")
    implementation('javax.annotation:javax.annotation-api:1.3.2')
    implementation("io.micronaut:micronaut-http-server-netty:$micronautVersion")
    implementation("io.micronaut:micronaut-http-client:$micronautVersion")
    implementation('io.swagger.core.v3:swagger-annotations:2.1.11')
    implementation("io.micronaut:micronaut-tracing:$micronautVersion")
    runtimeOnly('io.jaegertracing:jaeger-thrift:1.6.0')
    implementation('io.micronaut.mongodb:micronaut-mongo-reactive:4.0.0')
    implementation("io.micronaut.security:micronaut-security:3.2.0")
    implementation("io.micronaut.security:micronaut-security-jwt:3.2.0")
    implementation("io.micronaut.kotlin:micronaut-kotlin-extension-functions:3.0.0")
    implementation("io.micronaut.cache:micronaut-cache-caffeine:3.0.0")
    implementation('org.apache.logging.log4j:log4j-core:2.17.0')
    implementation('com.google.code.gson:gson:2.8.9')
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2-native-mt')
    implementation('com.jcraft:jsch:0.1.55')
    // NoSQL
    implementation(platform("org.dizitart:nitrite-bom:4.0.1-SNAPSHOT"))
    implementation('org.dizitart:nitrite')
    implementation('org.dizitart:nitrite-mvstore-adapter')
    implementation('org.dizitart:potassium-nitrite')
    // Shell commands
    implementation('com.lordcodes.turtle:turtle:0.6.0')
    runtimeOnly('org.apache.logging.log4j:log4j-api:2.17.0')
    runtimeOnly('org.apache.logging.log4j:log4j-slf4j-impl:2.17.0')
    runtimeOnly('com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1')
    runtimeOnly('io.netty:netty-tcnative:2.0.46.Final')
    runtimeOnly('io.netty:netty-tcnative-boringssl-static:2.0.46.Final')
    implementation("org.reflections:reflections:0.10.2")
    kaptTest(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    kaptTest("io.micronaut:micronaut-inject-java:$micronautVersion")
    testImplementation(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.2')
    testImplementation("io.micronaut.test:micronaut-test-junit5:$micronautVersion")
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.2')
    testImplementation('de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.2.4')
}

test.classpath += configurations.developmentOnly

mainClassName = "technology.iatlas.spaceup.ApplicationKt"

// use JUnit 5 platform
test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.toVersion('8')
}

allOpen {
    annotation("io.micronaut.aop.Around")
}
compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        //Will retain parameter names for Java reflection
        javaParameters = true
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        javaParameters = true
    }
}
kapt {
    arguments {
        arg("micronaut.processing.incremental", true)
        arg("micronaut.processing.annotations", "technology.iatlas.spaceup.*")
        arg("micronaut.processing.group", "technology.iatlas.spaceup")
        arg("micronaut.processing.module", "spaceUp")
        arg("micronaut.openapi.views.spec",
                "redoc.enabled=true,rapidoc.enabled=true," +
                        "swagger-ui.enabled=true,swagger-ui.theme=flattop")
    }
}

def generatedVersionDir = "${buildDir}/generated-version"
sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generatedVersion')
    }
}


tasks.register("generatedVersion") {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources {
    dependsOn tasks.named("generatedVersion")
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    if (gradle.startParameter.continuous) {
        systemProperties(
            'micronaut.io.watch.restart':'true',
            'micronaut.io.watch.enabled':'true',
            "micronaut.io.watch.paths":"src/main"
        )
    }
}

apply from: 'gradle/asciidoc.gradle'

publishing {
    publications {
        maven(MavenPublication) {
            artifact("build/libs/SpaceUp-$version"+"-all.jar") {
                extension 'jar'
            }
        }
    }
    repositories {
        maven {
            name "nexus"
            url "https://artifactory.iatlas.dev/repository/technology.iatlas.spaceup/"
            credentials {
                username "${nexusUsername}"
                password "${nexusPassword}"
            }
        }
    }
}

